/*
 * Copyright 2017 Jari Hämäläinen / https://github.com/nuumio
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

group 'fi.nuumio'
def versionStr = '0.1'
version "$versionStr"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

// For Android compatibility:
targetCompatibility = '1.7'
sourceCompatibility = '1.7'

repositories {
    jcenter()
}

dependencies {
    compile 'commons-cli:commons-cli:1.3.1'
    testCompile 'junit:junit:4.12'
}

// Limit docs to classes that client uses. At least until docs get better.
task netsyncJavadoc(type:Javadoc) {
    source = sourceSets.main.allJava
    include 'fi/nuumio/netsync/*.java'
    include 'fi/nuumio/netsync/client/*.java'
    include 'fi/nuumio/netsync/util/ClientSettings.java'
    include 'fi/nuumio/netsync/util/NodeId.java'
    include 'fi/nuumio/netsync/util/Token.java'
    include 'fi/nuumio/netsync/protocol/message/group/SyncType.java'
    // This will remove warnings of not found symbols and packages (means we need our own and our
    // dependencies at classpath).
    classpath = files(sourceSets.main.output)
    classpath += sourceSets.main.compileClasspath
}

task javadocJar(type: Jar, dependsOn: netsyncJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
}   from sourceSets.main.allSource


publishing {
    publications {
        NetSyncPublication(MavenPublication) {
            groupId = group
            artifactId = project.getName()
            version = version
            artifact(sourcesJar)
            artifact(javadocJar)
            from components.java
            pom.withXml {
                def root = asNode()
                root.children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    scm {
                        connection 'scm:git:https://github.com/nuumio/netsync.git'
                        developerConnection 'scm:git:https://github.com/nuumio/netsync.git'
                        url 'https://github.com/nuumio/netsync'
                    }
                    licenses {
                        license {
                            name 'Apache License, Version 2.0'
                            url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'nuumio'
                            name 'Jari Hämäläinen'
                            email 'nuumiofi@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['NetSyncPublication']
    pkg {
        repo = 'maven-public'
        name = project.getName()
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/nuumio/netsync.git'
        version {
            name = versionStr
        }
    }
}
